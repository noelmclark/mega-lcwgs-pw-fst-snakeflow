
localrules: make_bamlists,fold_winsfs,summarise_average_fst_values

include: "rules/common.smk"
include: "rules/winsfs-route.smk"
include: "rules/summarize.smk"
include: "rules/one-d-sfs-edification.smk"
include: "rules/diversity.smk"


rule all:
  input:
    aves=expand("results/{mode}/summarized/all-pw-fsts.tsv", mode=config["modes"]),
    thetaaves=expand("results/{mode}/summarized/all-thetas.tsv", mode = config["modes"]),
    windows=ALLSLIDEWINDOWS,
    thetawindows=ALLTHETAWIN


rule index_fasta:
  input:
    genome=config["genome"]
  output:
    fai=config["genome"] + ".fai"
  log:
    "results/logs/index_fasta/log.txt"
  conda:
    "envs/samtools.yaml"
  benchmark:
    "results/benchmarks/index_fasta/log.txt"
  shell:
    " samtools faidx {input.genome} 2> {log} "
    

rule make_bamlists:
  input:
    bl=get_bams_in_pop
  output:
    bl="results/{mode}/{chunk}/bamlists/{grp}.txt"
  log:
    "results/logs/make_bamlists/{mode}/{chunk}/{grp}.txt"
  benchmark:
    "results/benchmarks/make_bamlists/{mode}/{chunk}/{grp}.txt"
  shell:
    "echo {input.bl} | awk '{{for(i=1;i<=NF;i++) print $i}}' > {output.bl} 2> {log} "


rule calc_saf:
  input: 
    genome=config["genome"],
    fai=config["genome"] + ".fai",
    bl="results/{mode}/{chunk}/bamlists/{grp}.txt"
  params:
    bf=config["params"]["angsd_bam_filters"],
    cs=config["params"]["calc_saf"],
    chunk_opt=get_chunk
  output:
    saf="results/{mode}/{chunk}/saf/{grp}.saf.idx"
  threads: 1
  conda:
    "envs/angsd.yaml"
  log:
    "results/logs/calc_saf/{mode}/{chunk}/{grp}.txt"
  benchmark:
    "results/benchmarks/calc_saf/{mode}/{chunk}/{grp}.txt"
  shell:
    " PREFIX=$(echo {output.saf} | sed 's/\.saf\.idx$//g;');"
    " angsd -b {input.bl} -nThreads {threads} -anc {input.genome} {params.chunk_opt} "
    " -ref {input.genome} -out $PREFIX {params.cs} {params.bf} > {log} 2>&1 "



rule calc_2dsfs:
  input:
    saf1="results/{mode}/{chunk}/saf/{p1}.saf.idx",
    saf2="results/{mode}/{chunk}/saf/{p2}.saf.idx",
  output:
    sfs="results/{mode}/{chunk}/sfs/{p1}--x--{p2}.ml"
  params:
    rs=config["params"]["realSFS"],
    chunk_opt=get_chunk
  threads: 1
  conda:
    "envs/angsd.yaml"
  log:
    "results/logs/calc_2dsfs/{mode}/{chunk}/{p1}--x--{p2}.txt"
  benchmark:
    "results/benchmarks/calc_2dsfs/{mode}/{chunk}/{p1}--x--{p2}.txt"
  shell:
    " realSFS -cores {threads} {params.rs} {params.chunk_opt} {input.saf1} {input.saf2} > {output.sfs} 2>{log}"


rule calc_fst_binaries:
  input:
    saf1="results/{mode}/{chunk}/saf/{p1}.saf.idx",
    saf2="results/{mode}/{chunk}/saf/{p2}.saf.idx",
    sfs="results/{mode}/{chunk}/sfs/{p1}--x--{p2}.ml",
  params:
    rs=config["params"]["realSFS"],
    chunk_opt=get_chunk
  output:
    "results/{mode}/{chunk}/fst_bin/{p1}--x--{p2}.fst.idx"
  threads: 1
  conda:
    "envs/angsd.yaml"
  log:
    "results/logs/calc_fst_binaries/{mode}/{chunk}/{p1}--x--{p2}.txt"
  benchmark:
    "results/benchmarks/calc_fst_binaries/{mode}/{chunk}/{p1}--x--{p2}.txt"
  shell:
    " PREFIX=$(echo {output} | sed 's/\.fst\.idx$//g;');                             "
    " realSFS fst index -cores {threads} {params.rs} {params.chunk_opt} {input.saf1} {input.saf2} -sfs {input.sfs} -fstout $PREFIX > {log} 2>&1  "



rule extract_fst_values:
  input:
    "results/{mode}/{chunk}/fst_bin/{p1}--x--{p2}.fst.idx"
  output:
    "results/{mode}/{chunk}/fst/{p1}--x--{p2}.txt"
  conda:
    "envs/angsd.yaml"
  log:
    "results/logs/extract_fst_values/{mode}/{chunk}/{p1}--x--{p2}.txt"
  benchmark:
    "results/benchmarks/extract_fst_values/{mode}/{chunk}/{p1}--x--{p2}.txt"
  shell:
    "realSFS fst stats {input} > {output} 2>{log} "
    


